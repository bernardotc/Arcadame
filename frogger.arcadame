SimpleGame Frogger:
    SpriteSet:
        log = Missile orientation=left, speed=0.1, color=brown;
        forestDense = Spawner generatedSprite=log, prob=0.4, cooldown=10;
        forestSparse = Spawner generatedSprite=log, prob=0.1,  cooldown=5;
        water = Immovable color=blue;
        goal  = Immovable color=purple, portal=true;
        fasttruck = Missile orientation=right, speed=0.2,  color=orange;
        slowtruck = Missile orientation=right, speed=0.1,  color=red;
        city = Spawner generatedSprite=fasttruck, prob=0.5, cooldown=4;
        city2 = Spawner generatedSprite=slowtruck, prob=0.6, cooldown=8;
        wall = Immovable color=black;
        floor = Immovable color=white;
        avatar = MovingAvatar color=green;
    end
        
    InteractionList:
        goal encounters avatar = killSprite(avatar), scoreChange(1.0);
        avatar encounters log = shieldFrom(killSprite, water), pullWithIt;
        avatar encounters wall = stepBack;
        avatar, fasttruck encounters water = killSprite();
        avatar, slowtruck encounters water = killSprite();
        log encounters wall = killSprite();
        fasttruck encounters wall = killSprite();
        slowtruck encounters wall = killSprite();
    end
    
    TerminationGoals:
        spriteCounter sprite=goal   limit=0 win=true;
        spriteCounter sprite=avatar limit=0 win=false;
    end
    
    Mapping:
        'g' = goal;
        '0' = water;
        '1' = forestDense, water;
        '2' = forestDense, wall, log;
        '3' = forestSparse, water;
        '4' = forestSparse, wall, log;
        '-' = slowtruck;
        'x' = fasttruck;
        '=' = log, water;
        'A' = avatar;
        'c' = city;
        'C' = city2;
        'w' = wall;
        'f' = floor;
    end

    Map:
        "wwwwwwwwwwwwwwwwwwwwwwwwwwww";
        "wfffffffffffwgwffffffffffffw";
        "w00==000000===0000=====000=4";
        "w0000====0000000000====00012";
        "w00===000===000====0000===04";
        "wwwfffwwfffwwwffffwwwffwwwww";
        "wfff----fff---fff-ff----fffw";
        "w-fffffxxxfffffffxxxffffxxfw";
        "wf-fff---fffff-fff----f--ffw";
        "wfffffffAffffffffffffffffffw";
        "wwwwwwwwwwwwwwwwwwwwwwwwwwww";
    end

    Main:
        StartGame();
    end
End

