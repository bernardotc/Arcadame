SimpleGame Frogger:
    SpriteSet:
        log = Missile orientation=left, speed=4.0, color=brown;
        forestDense = Spawner generatedSprite=log, prob=0.4, cooldown=3;
        forestSparse = Spawner generatedSprite=log, prob=0.1,  cooldown=1;
        water = Immovable color=blue;
        goal  = Immovable color=purple, portal=true;
        fasttruck = Missile orientation=left, speed=6.0,  color=orange;
        slowtruck = Missile orientation=right, speed=3.0,  color=red;
        city = Spawner generatedSprite=fasttruck, prob=0.5, cooldown=2;
        city2 = Spawner generatedSprite=slowtruck, prob=0.7, cooldown=2;
        wall = Immovable color=black;
        floor = Immovable color=white;
        avatar = MovingAvatar color=green;
    end
        
    InteractionList:
        goal encounters avatar = killSprite(), scoreChange(1.0);
        avatar encounters log = shieldFrom(killSprite, water), pullWithIt;
        avatar encounters wall = stepBack;
        avatar encounters water = killSprite();
        log encounters wall = killSprite();
        fasttruck encounters wall = killSprite();
        slowtruck encounters wall = killSprite();
        slowtruck encounters avatar = killSprite(avatar);
        fasttruck encounters avatar = killSprite(avatar);
    end
    
    TerminationGoals:
        spriteCounter sprite=goal   limit=0 win=true;
        spriteCounter sprite=avatar limit=0 win=false;
    end
    
    Mapping:
        'g' = goal;
        '0' = water;
        '1' = forestDense, water;
        '3' = forestSparse, water;
        '-' = slowtruck;
        'x' = fasttruck;
        '=' = water, log;
        'A' = avatar;
        'c' = floor, city;
        'C' = floor, city2;
        'w' = wall;
        'f' = floor;
    end

    Map:
        "wwwwwwwwwwwwwwwwwwwwwwwwwwww";
        "wfffffffffffwgwffffffffffffw";
        "w00==000000===0000=====0003w";
        "w0000====0000000000====00=1w";
        "w00===000===000====0000===3w";
        "wwwfffwwfffwwwffffwwwffwwwww";
        "wCff----fff---fff-ff----fffw";
        "wxxxxffxxxfffffffxxxffffxxcw";
        "wC-fff---fffff-fff----f--ffw";
        "wfffffffAffffffffffffffffffw";
        "wwwwwwwwwwwwwwwwwwwwwwwwwwww";
    end

    Main:
        StartGame();
    end
End

